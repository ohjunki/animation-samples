/*
 * Copyright 2019 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        minSdkVersion = 21
        buildToolsVersion = "28.0.3"
        compileSdkVersion = 28
        targetSdkVersion = 28
        kotlin_version = '1.3.50'
        navigation_version = '2.2.0-alpha03'

        appCompatVersion = '1.1.0-alpha03'
        constraintLayoutVersion = '2.0.0-beta7'
        glideVersion = '4.8.0'
        kotlinVersion = '1.3.11'
        lifeCycleVersion = '2.2.0'
        lottieVersion = '2.5.1'
        materialVersion = '1.1.0-alpha05'
        junitVersion = '4.12'
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation_version"
    }
}

subprojects { pr ->
    repositories {
        google()
        jcenter()
    }

    if( pr.name != 'libAndroidUtils' ){
        apply plugin: 'com.android.application'
    }else{
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'

    android{
        compileSdkVersion rootProject.compileSdkVersion
        buildToolsVersion rootProject.buildToolsVersion

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        if( pr.name != 'libAndroidUtils' ){
            dependencies{
                implementation project(path: ':libAndroidUtils')
            }
        }

        defaultConfig{
            minSdkVersion rootProject.minSdkVersion
            targetSdkVersion rootProject.targetSdkVersion
        }
    }
}